## 202111. 读写 Binary 文件

2021-10-25

### 01. 参考资料

[Read/Write Binary Files - AutoLISP, Visual LISP & DCL - AutoCAD Forums](https://www.cadtutor.net/forum/topic/29079-readwrite-binary-files/)

```
;;-----------------=={ Read Binary Stream }==-----------------;;
;;                                                            ;;
;;  Uses the ADO Stream Object to read a supplied file and    ;;
;;  returns a variant of bytes.                               ;;
;;------------------------------------------------------------;;
;;  Author: Lee Mac, Copyright © 2011 - www.lee-mac.com       ;;
;;------------------------------------------------------------;;
;;  Arguments:                                                ;;
;;  filename - filename of file to read.                      ;;
;;  len      - number of bytes to read                        ;;
;;  (if non-numerical, less than 1, or greater than the size  ;;
;;   of the file, everything is returned).                    ;;
;;------------------------------------------------------------;;
;;  Returns:                                                  ;;
;;  Variant of Binary data which may be converted to a list   ;;
;;  bytes using the relevant VL Variant functions or used     ;;
;;  with LM:WriteBinaryStream.                                ;;
;;------------------------------------------------------------;;

(defun LM:ReadBinaryStream ( filename len / ADOStream result ) (vl-load-com)
 
 (setq result
   (vl-catch-all-apply
     (function
       (lambda ( / size )
         (setq ADOStream (vlax-create-object "ADODB.Stream"))
         (vlax-invoke ADOStream 'Open)
         (vlax-put-property   ADOStream 'type 1)
         (vlax-invoke-method  ADOStream 'loadfromfile filename)
         (vlax-put-property   ADOStream 'position 0)
         (setq size (vlax-get ADOStream 'size))
         (vlax-invoke-method  ADOStream 'read (if (and (numberp len) (< 0 len size)) (fix len) -1))
       )
     )
   )
 )
 (if ADOStream (vlax-release-object ADOStream))

 (if (not (vl-catch-all-error-p result))
   result
 )
)


;;-----------------=={ Write Binary Stream }==----------------;;
;;                                                            ;;
;;  Uses the ADO Stream Object to write a variant of bytes to ;;
;;  a specified file. File is created if non-existent or      ;;
;;  overwritten if found.                                     ;;
;;------------------------------------------------------------;;
;;  Author: Lee Mac, Copyright © 2011 - www.lee-mac.com       ;;
;;------------------------------------------------------------;;
;;  Arguments:                                                ;;
;;  filename - filename of file to read.                      ;;
;;  data     - variant of binary data to write to the file.   ;;
;;             (as returned by LM:ReadBinaryStream)           ;;
;;------------------------------------------------------------;;
;;  Returns:  Filename of specified file, else nil.           ;;
;;------------------------------------------------------------;;

(defun LM:WriteBinaryStream ( filename data / ADOStream result ) (vl-load-com)
 
 (setq result
   (vl-catch-all-apply
     (function
       (lambda ( )
         (setq ADOStream (vlax-create-object "ADODB.Stream"))
         (vlax-put-property ADOStream 'type 1)
         (vlax-invoke ADOStream 'open)
         (vlax-invoke-method ADOStream 'write data)
         (vlax-invoke ADOStream 'savetofile filename 2)
       )
     )
   )
 )
 (if ADOStream (vlax-release-object ADOStream))

 (if (not (vl-catch-all-error-p result))
   file
 )
)
```

[Read / Write Stream ...](http://www.theswamp.org/index.php?topic=17465.0)

```
(defun _ReadStream ( path len / fso file stream result )

    ;;  If the file is successful read the data is returned as
    ;;  a string. Won't be tripped up by nulls, control chars
    ;;  including ctrl z (eof marker). Pretty fast (feel free
    ;;  to bench mark / compare to alternates).
    ;;
    ;;  If the caller wants the result as a list of byte values
    ;;  simply use vl-string->list on the result:
    ;;
    ;;      (setq bytes
    ;;          (if (setq stream (_ReadStream path len))
    ;;              (vl-string->list stream)
    ;;          )
    ;;      )           
    ;;
    ;;  Arguments:
    ;;
    ;;      path  <duh>
    ;;      len   Number of bytes to read. If non numeric, less
    ;;            than 1 or greater than the number of bytes in
    ;;            the file everything is returned.
   
    (vl-catch-all-apply
       '(lambda ( / iomode format size )
            (setq
                iomode   1 ;; 1 = read, 2 = write, 8 = append
                format   0 ;; 0 = ascii, -1 = unicode, -2 = system default
                fso      (vlax-create-object "Scripting.FileSystemObject")
                file     (vlax-invoke fso 'GetFile path)
                stream   (vlax-invoke fso 'OpenTextFile path iomode format)
                size     (vlax-get file 'Size)
                len      (if (and (numberp len) (< 0 len size)) (fix len) size)
                result   (vlax-invoke stream 'read len)
            )
            (vlax-invoke stream 'Close)
        )
    )
   
    (if stream (vlax-release-object stream))
    (if file (vlax-release-object file))
    (if fso (vlax-release-object fso))
   
    result

)


(defun _WriteStream ( path text mode / fso stream file result )

    ;;  Return the file size if the file is successfully written
    ;;  to, otherwise nil. Will write all ascii chars to file
    ;;  including nulls. If the caller wants to pass a list of
    ;;  byte values to the function just call it like so:
    ;;
    ;;      (_WriteStream
    ;;          path
    ;;          (vl-list->string '(87 111 111 116 33))
    ;;          mode
    ;;      )
    ;;
    ;;  Arguments:
    ;;
    ;;      path  <duh>
    ;;      text  <duh>
    ;;      mode  "a" to create/append,
    ;;            "w" to create/overwrite (default)
   
    (setq mode (if (member mode '("a" "A")) "a" "w"))
   
    (vl-catch-all-apply
       '(lambda ( / format )
            (setq fso (vlax-create-object "Scripting.FileSystemObject"))
            (cond
                (   (or (null (findfile path)) (eq "w" mode))
                    (setq stream
                        (vlax-invoke
                            fso
                           'CreateTextFile
                            path
                           -1 ;; 0 (false) = don't overwrite , -1 (true) = overwrite
                            0 ;; 0 (false) = ascii, -1 (true) = unicode
                        )
                    )
                    (setq file (vlax-invoke fso 'GetFile path))
                )
                (   (setq file (vlax-invoke fso 'GetFile path))
                    (setq stream
                        (vlax-invoke
                            file
                           'OpenAsTextStream
                            8 ;; 1 = read, 2 = write, 8 = append
                            0 ;; 0 = ascii, -1 = unicode, -2 system default
                        )
                    )       
                )
            )
            (vlax-invoke stream 'Write text)
            (vlax-invoke stream 'Close)
            (setq result (vlax-get file 'Size))
        )
    )

    (if file (vlax-release-object file))
    (if stream (vlax-release-object stream))
    (if fso (vlax-release-object fso))
   
    result
   
)

(defun c:Test ( / tempPath stream1 stream2 )

    (setq stream1 ;; all ascii codes 0 - 255
        (   (lambda ( / i lst )
                (setq i 256)
                (repeat 256
                    (setq lst
                        (cons
                            (setq i (1- i))
                            lst
                        )
                    )
                )
                (vl-list->string lst)
            )
        )               
    )
   
    (_WriteStream
        (setq tempPath (vl-filename-mktemp))   
        stream1
        "w"
    )
   
    (setq stream2
        (_ReadStream
            tempPath
            t ;; get everything
        )
    )
   
    ;;  does stream1 = stream2 ??
   
    (princ
        (strcat
            "Streams are "
            (if (equal stream1 stream2)
                "equal.\n"
                "not equal.\n"
            )
        )
    )
   
    ;;  show the byte values
   
    (print (vl-string->list stream1))
    (princ "\n")
    (print (vl-string->list stream2))
   
    ;;  delete the temp file
   
    (vl-file-delete tempPath)
   
    (princ)   
   
)
```